start
  = w statements w

statements
  = s:statement* { return new Statements(s)}

statement
  = assignmentStatement
  / whileStatement

assignmentStatement
  = i:ident w "=" w e:expression w { return new AssignmentStatement(i, e); }

whileStatement
  = "while" w "(" w c:expression w ")" w "do" w "{" w s:statements w "}" w { return new WhileStatement(c, s); }

expression
  = additive
  / multiplicative

additive
  = left:multiplicative w "+" w right:additive { return new BinaryExpression(left, right, "+"); }
  / left:multiplicative w "-" w right:additive { return new BinaryExpression(left, right, "-"); }
  / multiplicative

multiplicative
  = left:primary w "*" w right:multiplicative { return new BinaryExpression(left, right, "*"); }
  / left:primary w "/" w right:multiplicative { return new BinaryExpression(left, right, "/"); }
  / primary

value
  = i:ident { return new ValueExpression(i); }

primary
  = value
  / integer
  / "(" w additive:additive w ")" { return additive; }

integer "integer"
  = digits:[0-9]+ { return new Integer(parseInt(digits.join(""), 10)); }

ident
  = name:[a-z]+ { return new Ident(name.join("")) }

w
  = [ \t\n\r]*
